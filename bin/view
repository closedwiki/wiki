#!/usr/bin/perl -wT
#
# TWiki WikiClone (see TWiki.pm for $wikiversion and other info)
#
# Based on parts of Ward Cunninghams original Wiki and JosWiki.
# Copyright (C) 1998 Markus Peter - SPiN GmbH (warpi@spin.de)
# Some changes by Dave Harris (drh@bhresearch.co.uk) incorporated
# Copyright (C) 1999-2001 Peter Thoeny, peter@thoeny.com
#
# This program is free software; you can redistribute it and/or
# modify it under the terms of the GNU General Public License
# as published by the Free Software Foundation; either version 2
# of the License, or (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details, published at 
# http://www.gnu.org/copyleft/gpl.html

use CGI::Carp qw( fatalsToBrowser );
use CGI;
use lib ( '.' );
use lib ( '../lib' );
use TWiki;

use strict;

writeDebugTimes( "view:start" );

##### for debug only: Remove next 2 comments (but redirect does not work)
#print "Content-type: text/html\n\n";
#open(STDERR,'>&STDOUT'); # redirect error to browser
#$| = 1;                  # no buffering

&main();

sub writeDebug
{
    my( $text ) = @_;
    #TWiki::writeDebug( $text );
}

sub writeDebugTimes
{
    my( $text ) = @_;
    
    #TWiki::writeDebugTimes( $text );
}

sub main
{
    my $query= new CGI;

    my $thePathInfo = $query->path_info(); 
    my $theRemoteUser = $query->remote_user();
    my $theTopic = $query->param( 'topic' );
    my $theUrl = $query->url;

    my $doShowAttachmentAttr = $query->param( 'showAttr' );

    my( $topic, $webName, $scriptUrlPath, $userName ) = 
	&TWiki::initialize( $thePathInfo, $theRemoteUser, $theTopic, $theUrl, $query );

    writeDebugTimes( "view - initialized" );

    my $tmpl = "";
    my $text = "";
    my @meta = ();
    my $rev = $query->param( "rev" );
    my $maxrev = 1;
    my $extra = "";
    my $wikiUserName = &TWiki::userToWikiName( $userName );
    my $revdate = "";
    my $revuser = "";

    if( ! &TWiki::Store::webExists( $webName ) ) {
        $tmpl= &TWiki::Store::readTemplate( "noweb" );
        $tmpl = &TWiki::handleCommonTags( $tmpl, $topic );
        TWiki::writeHeader( $query );
        print $tmpl;
        return;
    }

    my $unlock = $query->param( "unlock" ) || "";
    if( $unlock eq "on" ) {
        # unlock topic, user cancelled out of edit
        &TWiki::Store::lockTopic( $topic, "on" );
    }

    writeDebugTimes( "view - webExists" );

    # get view template, standard view or a view with a different skin
    my $skin = $query->param( "skin" ) || &TWiki::Prefs::getPreferencesValue( "SKIN" );
    $tmpl = &TWiki::Store::readTemplate( "view", $topic, $skin );
    if( ! $tmpl ) {
        TWiki::writeHeader( $query );
        print "<html><body>";
        print "<h1>TWiki Installation Error</h1>";
        print "Template directory $TWiki::templateDir not found.<p>";
        print "Check the \$templateDir variable in TWiki.cfg.";
        print "</body></html>";
        return;
    }

    writeDebugTimes( "view - readTemplate" );

    # Most recent topic read in even if earlier topic requested - makes code simpler and performance impact should be minimal
    my $topicExists = &TWiki::Store::topicExists( $webName, $topic );
    if( $topicExists ) {
        ( $text, @meta ) = &TWiki::Store::readWebTopicNew( $webName, $topic );
        ( $revdate, $revuser, $maxrev ) = &TWiki::Store::getRevisionInfoFromMeta( $webName, $topic, \@meta, "isoFormat" );
        writeDebug( "maxrev = $maxrev" );
        if( $rev ) {
            $rev =~ s/1\.//go;  # cut major
            if( $rev < 1 )       { $rev = 1; }
            if( $rev > $maxrev ) { $rev = $maxrev; }
        } else {
            $rev = $maxrev;
        }

        if( $rev < $maxrev ) {
            ( $text, @meta ) = &TWiki::Store::readVersion( $webName, $topic, "1.$rev" );
            ( $revdate, $revuser ) = &TWiki::Store::getRevisionInfoFromMeta( $webName, $topic, \@meta );
            $extra .= "r1.$rev";
        }

    } else {
        $rev = 1;
        if( &TWiki::isWikiName( $topic ) ) {
            $text= &TWiki::Store::readTemplate( "notext" );
        } else {
            $text= &TWiki::Store::readTemplate( "notwiki" );
        }
        $extra .= " (not exist)";
    }

    writeDebugTimes( "view - get rev info" );

    # check access permission
    # To Do: Need to protect also %INCLUDE% and search
    my $viewAccessOK = &TWiki::Access::checkAccessPermission( "view", $wikiUserName, $text, $topic, $webName );
    if( ! $viewAccessOK ) {
        # user could not be authenticated, may be not logged in yet?
        my $viewauthFile = $ENV{'SCRIPT_FILENAME'};
        $viewauthFile =~ s|/view|/viewauth|o;
        if( ( ! $theRemoteUser ) && (-e $viewauthFile ) ) {
            # try again with authenticated viewauth script
            # instead of non authenticated view script
            my $url = $ENV{"REQUEST_URI"};
            if( $url ) {
                # $url i.e. is "twiki/bin/view.cgi/Web/Topic?cms1=val1&cmd2=val2"
                $url =~ s|/view|/viewauth|o;
                $url = "$TWiki::urlHost$url";
            } else {
                $url = "$TWiki::urlHost$scriptUrlPath/$viewauthFile/$webName/$topic";
            }
            print $query->redirect( $url );
            return;
        }
    }
    if( ! $viewAccessOK ) {
        my $url = &TWiki::getOopsUrl( $webName, $topic, "oopsaccessview" );
        print $query->redirect( $url );
        return;
    }

    writeDebugTimes( "view - checked access permissions" );

    ##&TWiki::writeDebug("view showAtt = $doShowAttachmentAttr");
    # FIXME a rather messy way of doing things - maybe extra info to handleCommonTags?
    $TWiki::Attach::showAttr = $doShowAttachmentAttr;

    $text = &TWiki::handleCommonTags( $text, $topic );
    writeDebugTimes( "view - handleCommonTags done" );
    $text = &TWiki::getRenderedVersion( $text );
    writeDebugTimes( "view - getRendereredVersion done" );

    if( $TWiki::doLogTopicView ) {
        # write log entry
        &TWiki::Store::writeLog( "view", "$webName.$topic", $extra );
    }

    if( $rev < $maxrev ) {
        # disable edit of previous revisions
        $tmpl =~ s/%EDITTOPIC%/Edit/go;
        $tmpl =~ s/%REVTITLE%/\(r1.$rev\)/go;
    } else {
        if( $topicExists ) {
            $tmpl =~ s/%EDITTOPIC%/<a href=\"$scriptUrlPath\/edit%SCRIPTSUFFIX%\/%WEB%\/%TOPIC%\"><b>Edit<\/b><\/a>/go;
            # remove the NOINDEX meta tag
            $tmpl =~ s/<META NAME="ROBOTS"[^>]*>//goi;
        } else {
            $tmpl =~ s/%EDITTOPIC%/<a href=\"$scriptUrlPath\/edit%SCRIPTSUFFIX%\/%WEB%\/%TOPIC%\"><b>Create<\/b><\/a>/go;

        }
        $tmpl =~ s/%REVTITLE%//go;
    }

    my $i = $maxrev;
    my $j = $maxrev;
    my $revisions = "";
    my $breakRev = 0;
    if( ( $TWiki::numberOfRevisions > 0 ) && ( $TWiki::numberOfRevisions < $maxrev ) ) {
        $breakRev = $maxrev - $TWiki::numberOfRevisions + 1;
    }
    while( $i > 0 ) {
        if( $i eq $rev) {
            $revisions = "$revisions | r1.$i";
        } else {
            $revisions = "$revisions | <a href=\"$scriptUrlPath/view%SCRIPTSUFFIX%/%WEB%/%TOPIC%?rev=1.$i\">r1.$i</a>";
        }
        if( $i != 1 ) {
            if( $i == $breakRev ) {
                $revisions = "$revisions | <a href=\"$scriptUrlPath/oops%SCRIPTSUFFIX%/%WEB%/%TOPIC%?template=oopsrev&amp;param1=1.$maxrev\">&gt;...</a>";
                $i = 1;
            } else {
                $j = $i - 1;
                $revisions = "$revisions | <a href=\"$scriptUrlPath/rdiff%SCRIPTSUFFIX%/%WEB%/%TOPIC%?rev1=1.$i&amp;rev2=1.$j\">&gt;</a>";
            }
        }
        $i = $i - 1;
    }
    $tmpl =~ s/%REVISIONS%/$revisions/go;

    if( $topicExists ) {
        $revuser = &TWiki::userToWikiName( $revuser );
        my $temp = &TWiki::getRenderedVersion( "r1.$rev - $revdate GMT - $revuser" );
        $tmpl =~ s/%REVINFO%/$temp/go;
    } else {
        $tmpl =~ s/%REVINFO%//go;
    }

    $tmpl = &TWiki::handleCommonTags( $tmpl, $topic );
    writeDebugTimes( "view - handleCommonTags for template done" );
    my $metaText = &TWiki::renderMetaData( $webName, $topic, \@meta );
    $tmpl =~ s/%TEXT%/$text\n$metaText/go;
    $tmpl =~ s|</*nop/*>||goi;   # remove <nop> tags (PTh 06 Nov 2000)
    TWiki::writeHeader( $query );
    print $tmpl;

    writeDebugTimes( "view - done" );
}

# EOF
