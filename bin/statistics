#!/usr/bin/perl -wT
#
# TWiki WikiClone (see wiki.pm for $wikiversion and other info)
#
# Copyright (C) 2000 Peter Thoeny, peter@thoeny.com
# Copyright (C) 2002 Richard Donkin, rdonkin@bigfoot.com
#
# This program is free software; you can redistribute it and/or
# modify it under the terms of the GNU General Public License
# as published by the Free Software Foundation; either version 2
# of the License, or (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details, published at 
# http://www.gnu.org/copyleft/gpl.html
# 
# DESCRIPTION
# This script does update the usage statistics of each TWiki web.
# It reads the current month's log file and updates the table
# in the WebStatistics topic of each web.
# The script should be called by a cron job, it is recommended to
# call it once a day.

use CGI::Carp qw(fatalsToBrowser);
use CGI;
use File::Copy qw(copy);
use IO::File;


use lib ( '.' );
use lib ( '../lib' );
use TWiki;

use strict;
# use diagnostics;		# Debug only

#open(STDERR,'>&STDOUT');   # redirect error to browser
# FIXME: Beware global $| in mod_perl!  Should use 'local'
$| = 1;                    # no buffering

# initialize variables
my $isCgi = "";
my $cgiQuery = "";

&main();

# =========================
sub main
{
    my $tmp;
    my $theTopic = "";
    my $thePathInfo = ""; 
    my $theRemoteUser = "";
    my $logDate = "";

    # determine at runtime if script is called by browser or cron job
    if( $ENV{'DOCUMENT_ROOT'} ) {
        # script is called by browser
        $isCgi = "1";
        $cgiQuery = new CGI;
        $thePathInfo = $cgiQuery->path_info() || ""; 
        $theRemoteUser = $cgiQuery->remote_user() || "";
        $theTopic = $cgiQuery->param( 'topic' ) || "";
        $tmp = $cgiQuery->param( 'logdate' ) || "";
        $tmp =~ s/[^0-9]//g;  # remove all non numerals
        if( $tmp ne "" ) {
            $logDate = "$tmp";
        }
        TWiki::writeHeader( $cgiQuery );
        print "<html>\n<head>\n<title>TWiki: Create Usage Statistics</title>\n";
        print "</head>\n<body>\n";
    } else {
        # script is called by cron job
    }

    # Initial messages
    printMsg( "TWiki: Create Usage Statistics" );
    if( $isCgi ) {
        print "<h4><font color=\"red\">Do not interrupt this script!</font> ( Please wait until page download has finished )</h4>\n";
    }
    if ( $theRemoteUser ) {
        $tmp = &TWiki::userToWikiName( $theRemoteUser );
        $tmp =~ s/^Main\.//;
        printMsg( "* Executed by $tmp" );
    } else {
        printMsg( "* Executed by a guest or a cron job scheduler" );
    }

    if( ! $logDate ) {
        # get current local time and format to "yyyymm" format:
        my ( $sec, $min, $hour, $mday, $mon, $year) = localtime( time() );
        $year = sprintf("%.4u", $year + 1900);  # Y2K fix
        $mon = $mon+1;
        $logDate = sprintf("%.4u%.2u", $year, $mon);
    }

    my $logMonth;
    my $logYear;
    $tmp = $logDate;
    $tmp =~ s/([0-9]{4})(.*)/$2/g;
    if( $tmp && $tmp < 13 ) {
        $logMonth = $TWiki::isoMonth[$tmp-1];
    } else {
        $logMonth = "Date error";
    }
    $logYear = $logDate;
    $logYear =~ s/([0-9]{4})(.*)/$1/g;
    my $logMonthYear = "$logMonth $logYear";
    printMsg( "* Statistics for $logMonthYear" );

    my $logFile = $TWiki::logFilename;
    $logFile =~ s/%DATE%/$logDate/g;

    if( -e $logFile ) {
	# Copy the log file to temp file, since analysis could take some time

	# FIXME move the temp dir stuff to TWiki.cfg
	my $tmpDir;
	if ( $TWiki::OS eq "UNIX" ) { 
	    $tmpDir = $ENV{'TEMP'} || "/tmp"; 
	} elsif ( $TWiki::OS eq "WINDOWS" ) {
	    $tmpDir = $ENV{'TEMP'} || "c:/"; 
	} else {
	    # FIXME handle other OSs properly - assume Unix for now.
	    $tmpDir = "/tmp";
	}
	my $randNo = int ( rand 1000);	# For mod_perl with threading...
	my $tmpFilename = "$tmpDir/twiki-stats.$$.$randNo";
	$tmpFilename =~ /(.*)/; $tmpFilename = $1;   # Untaint

	File::Copy::copy ($logFile, $tmpFilename)
	    or die "Can't copy $logFile to $tmpFilename - $!";

	# Open the temp file
	my $TMPFILE = new IO::File;
	open $TMPFILE, $tmpFilename 
	    or die "Can't open $tmpFilename - $!";

	# Do a single data collection pass on the temporary copy of logfile,
	# then call processWeb once for each web of interest.
	my ($viewRef, $contribRef, $statViewsRef, $statSavesRef, 
		$statUploadsRef) = collectLogData( $TMPFILE, $logMonthYear );

=pod
	# DEBUG ONLY
	debugPrintHash($viewRef);
	debugPrintHash($contribRef);
	print "statViews tests===========\n";
	print "Views in Main = " . ${$statViewsRef}{'Main'} . "\n";
	print "hash stats (used/avail) = " . %{$statViewsRef}."\n";

	foreach my $web (keys %{$statViewsRef}) {
	   print "Web summary for $web\n";
	   print $statViewsRef->{$web}."\n";
	   print $statSavesRef->{$web}."\n";
	   print $statUploadsRef->{$web}."\n";
	}
=cut

	# Generate WebStatistics topic update for one or more webs
        if ( $thePathInfo ) {
            # do a particular web:
            processWeb( $thePathInfo, $theRemoteUser, $theTopic, $logMonthYear, $viewRef, $contribRef, $statViewsRef, $statSavesRef, $statUploadsRef );

        } else {
            # do all webs:
            my $dataDir = &TWiki::getDataDir();
            my @weblist = ();
            if( opendir( DIR, "$dataDir" ) ) {
                @weblist = grep /^[^\.\_]/, readdir DIR; # exclude webs starting with . or _
                closedir DIR;
            } else {
                printMsg( "  *** Error: opendir $dataDir, $!" );
            }
            foreach my $web ( @weblist ) {
                if( -d "$dataDir/$web" ) {
                    processWeb( "/$web", $theRemoteUser, $theTopic, $logMonthYear, $viewRef, $contribRef, $statViewsRef, $statSavesRef, $statUploadsRef );
                }
            }
        }
	close $TMPFILE;		# Shouldn't be necessary with 'my'
	unlink $tmpFilename;	# FIXME: works on Windows???  Unlink before
				# usage to ensure deleted on crash?
    } else {
        printMsg( "  - Note: Log file $logFile does not exist" );
    }

    if( $isCgi ) {
        $tmp = $TWiki::statisticsTopicname;
        my $url = &TWiki::getViewUrl( "", $tmp );
        printMsg( "* Go back to <a href=\"$url\">$tmp</a> topic" );
        printMsg( "End creating usage statistics" );
        print "</body></html>\n";
    } else {
        printMsg( "End creating usage statistics" );
    }
}

# Debug only
# Print all entries in a view or contrib hash, sorted by web and item name
sub debugPrintHash {
    my ($statsRef) = @_;
    # print "Main.WebHome views = " . ${$statsRef}{'Main'}{'WebHome'}."\n";
    # print "Main web, TWikiGuest contribs = " . ${$statsRef}{'Main'}{'Main.TWikiGuest'}."\n";
    foreach my $web ( sort keys %$statsRef) {
	my $count = 0;
	print "$web web:\n";
	# Get reference to the sub-hash for this web
	my $webhashref = ${$statsRef}{$web};
		# print "webhashref is " . ref ($webhashref) ."\n";
	# Items can be topics (for view hash) or users (for contrib hash)
	foreach my $item ( sort keys %$webhashref ) {
	   print "  $item = ";
	   print "" . ( ${$webhashref}{$item} || 0 ) ."\n";
	   $count += ${$webhashref}{$item};
	}
	print "  WEB TOTAL = $count\n";
    }
}


# =========================
# Process the whole log file and collect information in hash tables.
# Main hash tables are divided by web:
#
#   $view{$web}{$TopicName} == number of views, by topic
#   $contrib{$web}{"Main.".$WikiName} == number of saves/uploads, by user

sub collectLogData
{
    my( $TMPFILE, $theLogMonthYear ) = @_;

    # format example of log:
    # | 03 Feb 2000 - 02:43 | Main.PeterThoeny | view | Know.WebHome |  |
    # | 03 Feb 2000 - 02:43 | Main.PeterThoeny | save | Know.WebHome |  |
    # | 03 Feb 2000 - 02:53 | Main.PeterThoeny | save | Know.WebHome | repRev 1.7 Main.PeterThoeny 2000/02/03 02:43:22 |
    # | 23 Feb 2002 - 11:07 | Main.TWikiGuest | search | Main | Office *Locations[^A-Za-z] | 127.0.0.1 |
    #   	Note: there's no topic name on search log entry


    my %view;		# Hash of hashes, counts topic views by <web, topic>
    my %contrib;	# Hash of hashes, counts uploads/saves by <web, user>

    # Hashes for each type of statistic, one hash entry per web
    my %statViews;
    my %statSaves;
    my %statUploads;

    my ($webName, $opName, $topicName, $userName);
    binmode $TMPFILE;
    while ( <$TMPFILE> ) {
        my $line = $_;
	$line =~ s/\r*\n$//o;		# Clean out line endings

	$line =~ /^\|[^\|]*\| ([A-Za-z0-9]+\.[A-Za-z0-9]+) \| ([A-Za-z0-9]+) \| ([A-Za-z0-9]+)[. ]/o;
	$userName = $1 || "";		# Main.FredBloggs
	$opName = $2 || "";
	$webName = $3 || "";
	if (!defined $1) {
	    TWiki::writeDebug("Invalid log file line = '$line'");
	    TWiki::writeDebug("userName = '$userName'");
	    TWiki::writeDebug("opName = '$opName'");
	    TWiki::writeDebug("webName = '$webName'");
	}

	my $logContrib = 0;
	if ($opName eq 'view' ) {
	    $statViews{$webName}++;
	    # Pick up the topic name - should be in this log entry
	    $line =~ /^\|[^\|]*\| ([A-Za-z0-9]+\.[A-Za-z0-9]+) \| ([A-Za-z0-9]+) \| ([A-Za-z0-9]+)\.([A-Za-z0-9]+) /o;
	    $topicName = $4;
	    $view{$webName}{$topicName}++;
	} elsif ($opName eq 'save' ) {
	    $statSaves{$webName}++;
	    $logContrib = 1;
	} elsif ($opName eq 'upload' ) {
	    $statUploads{$webName}++;
	    $logContrib = 1;
	}
	# Record saves and uploads
	if ($logContrib) {
	    # Record the contribution by user name
	    $contrib{$webName}{$userName}++;
	}
=pod
	# DEBUG
	$. <= 5 && print "$line\n";
	print "$line\n";
	print "$.: $userName did $opName on $webName";
	print ".$topicName" if (defined $topicName);
	print "\n";
=cut

    }

=pod

    print "Main.WebHome views = " . $view{'Main'}{'WebHome'}."\n";
    print "Main web's contribs = " . $contrib{'Main'}{'Main.RichardDonkin'}."\n";
    debugPrintHash(\%view);
    debugPrintHash(\%contrib);
=cut
    return \%view, \%contrib, \%statViews, \%statSaves, \%statUploads;




}

# =========================
sub processWeb
{
    my( $thePathInfo, $theRemoteUser, $theTopic, $theLogMonthYear, $viewRef, $contribRef, $statViewsRef, $statSavesRef, $statUploadsRef ) = @_;

    my ( $topic, $webName, $dummy, $userName, $dataDir ) = 
        &TWiki::initialize( $thePathInfo, $theRemoteUser, $theTopic, "", $cgiQuery );
    $dummy = "";  # to suppress warning

    printMsg( "* Reporting on TWiki.$webName web" );

    if( ! &TWiki::Store::webExists( $webName ) ) {
        printMsg( "  *** Error: Web $webName does not exist" );
        return;
    }

    # Handle null values, print summary message to browser/stdout
    my $statViews = $statViewsRef->{$webName};
    my $statSaves = $statSavesRef->{$webName};
    my $statUploads = $statUploadsRef->{$webName};
    $statViews ||= 0;
    $statSaves ||= 0;
    $statUploads ||= 0;
    printMsg( "  - view: $statViews, save: $statSaves, upload: $statUploads" );


    # Get the top N views and contribs in this web
    my (@topViews) = getTopList( $TWiki::statsTopViews, $webName, $viewRef );
    my (@topContribs) = getTopList( $TWiki::statsTopContrib, $webName, $contribRef );

    # Print information to stdout
    my $statTopViews = "";
    my $statTopContributors = "";
    if( @topViews ) {
	printMsg( "  - top view: $topViews[0]" );
	$statTopViews = join( "<br /> ", @topViews );
    }
    if( @topContribs ) {
	printMsg( "  - top contributor: $topContribs[0]" );
	$statTopContributors = join( "<br /> ", @topContribs );
    }

    # Update the WebStatistics topic

    my $tmp;
    my $statsTopic = $TWiki::statisticsTopicname;
    # DEBUG
    # $statsTopic = "TestStatistics";		# Create this by hand
    if( &TWiki::Store::topicExists( $webName, $statsTopic ) ) {
	my( $meta, $text ) = &TWiki::Store::readTopic( $webName, $statsTopic );
	my @lines = split( /\n/, $text );
	my $statLine;
	my $idxStat = -1;
	my $idxTmpl = -1;
	for( my $x = 0; $x < @lines; $x++ ) {
	    $tmp = $lines[$x];
	    # Check for existing line for this month+year
	    if( $tmp =~ /$theLogMonthYear/ ) {
		$idxStat = $x;
	    } elsif( $tmp =~ /<\!\-\-statDate\-\->/ ) {
		$statLine = $_;
		$idxTmpl = $x;
	    }
	}
	if( ! $statLine ) {
	    $statLine = "| <!--statDate--> | <!--statViews--> | <!--statSaves--> | <!--statUploads--> | <!--statTopViews--> | <!--statTopContributors--> |";
	}
	$statLine =~ s/<\!\-\-statDate\-\->/$theLogMonthYear/o;
	$statLine =~ s/<\!\-\-statViews\-\->/ $statViews/o;
	$statLine =~ s/<\!\-\-statSaves\-\->/ $statSaves/o;
	$statLine =~ s/<\!\-\-statUploads\-\->/ $statUploads/o;
	$statLine =~ s/<\!\-\-statTopViews\-\->/$statTopViews/o;
	$statLine =~ s/<\!\-\-statTopContributors\-\->/$statTopContributors/o;

	if( $idxStat >= 0 ) {
	    # entry already exists, need to update
	    $lines[$idxStat] = $statLine;

	} elsif( $idxTmpl >= 0 ) {
	    # entry does not exist, add after <!--statDate--> line
	    $lines[$idxTmpl] = "$lines[$idxTmpl]\n$statLine";

	} else {
	    # entry does not exist, add at the end
	    $lines[@lines] = $statLine;
	}
	$text = join( "\n", @lines );
	$text .= "\n";

	&TWiki::Store::saveTopic( $webName, $statsTopic, $text, $meta, "", 1, 1, 1 );
	printMsg( "  - Topic $statsTopic updated" );

    } else {
	printMsg( "  *** Warning: No updates done, topic $webName.$statsTopic does not exist" );
    }
}

# =========================
# Get the items with top N frequency counts
# Items can be topics (for view hash) or users (for contrib hash)
sub getTopList
{
    my( $theMaxNum, $webName, $statsRef ) = @_;

    # Get reference to the sub-hash for this web
    my $webhashref = $statsRef->{$webName};

    # print "Main.WebHome views = " . $statsRef->{$webName}{'WebHome'}."\n";
    # print "Main web, TWikiGuest contribs = " . ${$statsRef}{$webName}{'Main.TWikiGuest'}."\n";

    my @list = ();
    my $topicName;
    my $statValue;

    # Convert sub hash of item=>statsvalue pairs into an array, @list, 
    # of '$statValue $topicName', ready for sorting.
    while( ( $topicName, $statValue ) = each( %$webhashref ) ) {
	# Right-align statistic value for sorting
	$statValue = sprintf "%7d", $statValue;	
	# Add new array item at end	of array
	$list[@list] = "$statValue $topicName";
    }

    # DEBUG
    # print " top N list for $webName\n";
    # print join "\n", @list;

    # Sort @list by frequency and pick the top N entries
    if( @list ) {
        @list = map{ s/^([0-9][0-9][^0-9])/\&nbsp\;$1/o; $_ }
                map{ s/^([0-9][^0-9])/\&nbsp\;\&nbsp\;$1/o; $_ }
                map{ s/^\s*//o; $_ }
                reverse( sort( @list ) );
        if( $theMaxNum >= @list ) {
            $theMaxNum = @list - 1;
        }
        return @list[0..$theMaxNum];
    }
    return @list;
}

# =========================
sub printMsg
{
    my( $msg ) = @_;
    my $htmlMsg = $msg;

    if( $isCgi ) {
	if( $htmlMsg =~ /^[A-Z]/ ) {
	    $htmlMsg =~ s/^([A-Z].*)/<h3>$1<\/h3>/go;
	} else {
	    $htmlMsg =~ s/(\*\*\*.*)/<font color=\"FF0000\">$1<\/font>/go;
	    $htmlMsg =~ s/^\s\s/&nbsp;&nbsp;/go;
	    $htmlMsg =~ s/^\s/&nbsp;/go;
	    $htmlMsg .= "<br />";
	}
	$htmlMsg =~ s/==([A-Z]*)==/<font color=\"FF0000\">==$1==<\/font>/go;
        print "$htmlMsg\n";
    } else {
        print "$msg\n";
    }
}

# =========================
# EOF
