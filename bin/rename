#!/usr/bin/perl -wT
#
# TWiki WikiClone (see TWiki.pm for $wikiversion and other info)
#
# Copyright (C) 2001 Peter Thoeny, Peter@Thoeny.com
# Copyright (C) 2001 Sven Dowideit, svenud@ozemail.com.au
#
# This program is free software; you can redistribute it and/or
# modify it under the terms of the GNU General Public License
# as published by the Free Software Foundation; either version 2
# of the License, or (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details, published at
# http://www.gnu.org/copyleft/gpl.html
#

use CGI::Carp qw( fatalsToBrowser );
use CGI;
use lib ( '.' );
use lib ( '../lib' );
use TWiki;

use strict;

use vars qw($query);

&main();


sub main
{
    $query= new CGI;
    my $thePathInfo = $query->path_info(); 
    my $theRemoteUser = $query->remote_user();
    my $theTopic = $query->param( 'topic' );
    my $newWeb = $query->param( 'newweb' );
    my $newTopic = $query->param( 'newtopic' );
    my $theUrl = $query->url;
    my $lockFailure = "";
    my $breakLock = $query->param( 'breaklock' );
    my $theAttachment = $query->param( 'attachment' );
    my $confirm = $query->param( 'confirm' );
    
    if( ! $theAttachment ) {
        $theAttachment = "";
    }
    
    my ( $oldTopic, $oldWeb, $scriptUrlPath, $userName, $dataDir ) = 
        &TWiki::initialize( $thePathInfo, $theRemoteUser, $theTopic, $theUrl, $query ); # DRKW difference to core

    my $wikiUserName = &TWiki::userToWikiName( $userName );
    
    # justChangeRefs will be true when some topics that had links to $oldTopic
    # still need updating, previous update being prevented by a lock.
    my $justChangeRefs = $query->param( 'changeRefs' );
    if( ! $justChangeRefs ) {
       $justChangeRefs = "";
    }
    
    my $fileName = &TWiki::Store::getFileName($oldWeb, $oldTopic);
    my $newName;
    $newName = &TWiki::Store::getFileName($newWeb, $newTopic) if( defined( $newWeb ) );
    
    if( ! $justChangeRefs ) {
       if( checkExist( $oldWeb, $oldTopic, $newWeb, $newTopic, $theAttachment, $fileName, $newName ) ) {
           return;
       }
    
       if( ! checkPermissions( $oldWeb, $oldTopic, $wikiUserName ) ) {
           # FIXME: This produces Internal Server Error if oldWeb does not exist
           return;
       }
       
       # FIXME no point old and new topics being same (if in same WEB)
    }

    # Has user selected new name yet? FIXME are there too many checks above?
    if( ! $newTopic || $confirm ) {
       newTopicScreen( $oldWeb, $oldTopic, $newWeb, $newTopic, $theAttachment, $confirm );
       return;    
    } 
    
    if( ! $justChangeRefs ) {
        if( ! getLocks( $oldWeb, $oldTopic, $newWeb, $newTopic, $theAttachment, $breakLock ) ) {
              return;
        }
    }
            
    # FIXME should warn if newname is not a wiki name
    # FIXME to do after move?
    if( ! $theAttachment ) {
        my @refs = findReferingPages( $oldWeb, $oldTopic );
        my $problems;
        ( $lockFailure, $problems ) = 
            updateReferingPages( $oldWeb, $oldTopic, $wikiUserName, $newWeb, $newTopic, @refs );
    }

    if( ! $justChangeRefs ) {
       if( $theAttachment ) {
	  my $moveError = 
	     &TWiki::Store::moveAttachment( $oldWeb, $oldTopic, $newWeb, $newTopic, $theAttachment );
	  # FIXME change to doing as one call
	  if( $moveError ) {
	      TWiki::writeHeader( $query );
              my $tmpl = &TWiki::Store::readTemplate( "oopsgeneral" );
	      $tmpl = &TWiki::handleCommonTags( $tmpl, $newTopic ); # FIXME newtopic?
	      $tmpl =~ s/%SUMMARY%/problem moving attachment/go;
	      $tmpl =~ s/%DETAIL%/$moveError/go;
              print $tmpl;
              return;
          }
       } else {
	  my $renameError = &TWiki::Store::renameTopic( $oldWeb, $oldTopic, $newWeb, $newTopic, "relink" );
	  if( $renameError ) {
	      TWiki::writeHeader( $query );
	      my $tmpl = &TWiki::Store::readTemplate( "oopsgeneral" );
	      $tmpl = &TWiki::handleCommonTags( $tmpl, $newTopic ); #FIXME newTopic?
	      $tmpl =~ s/%SUMMARY%/problem renaming topic/go;
	      $tmpl =~ s/%DETAIL%/$renameError/go;
	      print $tmpl;
	      return;
           }
       } 
    }

    my $new_url = "";
    if( $lockFailure ) {
       moreRefsToChange( $oldWeb, $oldTopic, $newWeb, $newTopic );
       return;
    } else {
       #redirect to new topic
       $new_url = &TWiki::getViewUrl( $newWeb, $newTopic );
    }

    TWiki::redirect( $query, $new_url );
    return;
}


#=========================
sub findReferingPages
{
    my @result = ();
    
    # Go through parameters finding all topics for change
    my @types = qw\local global\;
    foreach my $type ( @types ) {
	my $count = 1;
	while( $query->param( "TOPIC$type$count" ) ) {
	   my $checked = $query->param( "RENAME$type$count" );
	   if ($checked) {
	      push @result, $type;
	      push @result, $query->param( "TOPIC$type$count" );
	   }
	   $count++;
	}
    }
    return @result;
}


#==================================
# update pages that refer to the one being renamed/moved
sub updateReferingPages
{
    my ( $oldWeb, $oldTopic, $wikiUserName, $newWeb, $newTopic, @refs ) = @_;

    my $lockFailure = 0;

    my $result = "";
    my $preTopic = '^|[\*\s\[][\(-\s]*';
    my $postTopic = '$|[^A-Za-z0-9_]';

    while ( @refs ) {
       my $type = shift @refs;
       my $item = shift @refs;
       my($itemWeb, $itemTopic) = TWiki::Store::getWebTopic($item);
       if ( &TWiki::Store::topicIsLockedBy( $itemWeb, $itemTopic ) ) {
          $lockFailure = 1;
       } else {
          my $resultText = "";
          $result .= ":$item: , "; 
          #open each file, replace $topic with $newTopic
          if ( &TWiki::Store::topicExists($itemWeb, $itemTopic) ) { 
             my( $meta, $scantext ) = &TWiki::Store::readTopic($itemWeb, $itemTopic);
             if( ! &TWiki::Access::checkAccessPermission( "change", $wikiUserName, $scantext,
                    $itemWeb, $itemTopic ) ) {
                 # This shouldn't happen, as search will not return, but check to be on the safe side
                 &TWiki::writeWarning( "rename: attempt to change $itemWeb.$itemTopic without permission" );
                 next;
             }
	     my $insidePRE = 0;
	     my $insideVERBATIM = 0;
             my $noAutoLink = 0;
	     foreach( split( /\n/, $scantext ) ) {
		# This code is in far too many places
		m|<pre>|i  && ( $insidePRE = 1 );
		m|</pre>|i && ( $insidePRE = 0 );
		if( m|<verbatim>|i ) {
		    $insideVERBATIM = 1;
		}
		if( m|</verbatim>|i ) {
		    $insideVERBATIM = 0;
		}
		m|<noautolink>|i   && ( $noAutoLink = 1 );
		m|</noautolink>|i  && ( $noAutoLink = 0 );

		if( ! ( $insidePRE || $insideVERBATIM || $noAutoLink ) ) {
		    if( $type eq "global" ) {
			my $insertWeb = ($itemWeb eq $newWeb) ? "" : "$newWeb.";
			s/($preTopic)\Q$oldWeb.$oldTopic\E(?=$postTopic)/$1$insertWeb$newTopic/g;
		    } else {
			# Only replace bare topic (i.e. not preceeded by web) if web of referring
			# topic is in original Web of topic that's being moved
			if( $oldWeb eq $itemWeb ) {
			    my $insertWeb = ($oldWeb eq $newWeb) ? "" : "$newWeb.";
			    s/($preTopic)\Q$oldTopic\E(?=$postTopic)/$1$insertWeb$newTopic/g;
			}
		    }
		}
	        $resultText .= "$_\n";
	     }
	     &TWiki::Store::saveTopic( $itemWeb, $itemTopic, $resultText, $meta, "", "unlock", "dontNotify", "" );
          } else {
	    $result .= ";$item does not exist;"; # FIXME is this needed?
          }
       }
    }
    return ( $lockFailure, $result );
}


#=============================
# return "" if problem, otherwise return text of oldTopic
sub checkPermissions
{
    my( $oldWeb, $oldTopic, $wikiUserName ) = @_;
   
    my $ret = "";
   
    if( &TWiki::Store::topicExists( $oldWeb, $oldTopic ) ) {
	$ret = &TWiki::Store::readWebTopic( $oldWeb, $oldTopic );
    }
    
    if( ! &TWiki::Access::checkAccessPermission( "change", $wikiUserName, $ret, $oldTopic, $oldWeb ) ) {
       # user has not permission to change the topic
       my $url = &TWiki::getOopsUrl( $oldWeb, $oldTopic, "oopsaccesschange" );
       TWiki::redirect( $query, $url );
       $ret = "";
    }
    
    
    if( ! &TWiki::Access::checkAccessPermission( "rename", $wikiUserName, $ret, $oldTopic, $oldWeb ) ) {
       my $url = &TWiki::getOopsUrl( $oldWeb, $oldTopic, "oopsaccessrename" );
       TWiki::redirect( $query, $url );
       $ret = "";
    }

    return $ret;
}


#==========================================
# Check that various web and topics exist or don't exist as required
sub checkExist
{
   my( $oldWeb, $oldTopic, $newWeb, $newTopic, $theAttachment, $oldFileName, $newFileName ) = @_;
   
   my $ret = "";
   my $tmpl = "";
   
   # Does old WEB exist?
   if( ! &TWiki::Store::webExists( $oldWeb ) ) {
      # FIXME: Below code needs also getRenderedVersion. Better to use redirect instead. 
      TWiki::writeHeader( $query ); 
      $tmpl = &TWiki::Store::readTemplate( "oopsnoweb" );
      $tmpl = &TWiki::handleCommonTags( $tmpl, $oldTopic, $oldWeb );
      print $tmpl;
      $ret = "problem";
   }

   # Does new WEB exist?
   if( defined( $newFileName ) && ! &TWiki::Store::webExists( $newWeb ) ) {
      TWiki::writeHeader( $query );
      $tmpl= &TWiki::Store::readTemplate( "oopsnoweb" );
      $tmpl = &TWiki::handleCommonTags( $tmpl, $newTopic, $newWeb );
      print $tmpl;
      $ret = "problem";
   }

   # Does old attachment exist?
   if( ! -e $oldFileName) {
      TWiki::writeHeader( $query );
      $tmpl= &TWiki::Store::readTemplate( "oopsmissing" );
      $tmpl = &TWiki::handleCommonTags( $tmpl, $oldTopic, $oldWeb );
      print $tmpl;
      $ret = "problem"; 
   }

   # Check new topic doesn't exist (opposite if we've moving an attachment)
   if( defined( $newFileName ) && -e $newFileName && ! $theAttachment ) {
      # Unless moving an attachment, new topic should not already exist
      TWiki::writeHeader( $query );
      # FIXME - should this use oopsexists rather than oopsgeneral?
      $tmpl = &TWiki::Store::readTemplate( "oopsgeneral" );
      $tmpl =~ s/%SUMMARY%/Topic $newTopic already exists/go;
      $tmpl =~ s|%DETAIL%|Topic $newTopic already exists in Web $newWeb, press <I>Back</I> and try a new name|go;
      # FIXME - could offer number addition to topic name that doesn't exist?
      $tmpl = &TWiki::handleCommonTags( $tmpl, $newTopic, $newWeb );
      print $tmpl;
      # FIXME error doesn't appear on user screen.
      $ret = "problem";    
   }    
   
   if( defined( $newFileName ) && $theAttachment && ! -e $newFileName ) {
      TWiki::writeHeader( $query );
      $tmpl= &TWiki::Store::readTemplate( "oopsmissing" );
      $tmpl = &TWiki::handleCommonTags( $tmpl, $newTopic, $newWeb );
      print $tmpl;
      $ret = "problem"; 
   }
   
   return $ret;
}


#============================
#Return "" if can't get lock, otherwise "okay"
sub getLocks
{
    my( $oldWeb, $oldTopic, $newWeb, $newTopic, $theAttachment, $breakLock ) = @_;
    
    my( $oldLockUser, $oldLockTime, $newLockUser, $newLockTime );
    
    if( ! $breakLock ) {
	# Check for lock - at present the lock can't be broken
	( $oldLockUser, $oldLockTime ) = &TWiki::Store::topicIsLockedBy( $oldWeb, $oldTopic );
	if( $oldLockUser ) {
	   $oldLockUser = &TWiki::userToWikiName( $oldLockUser );
	   use integer;
	   $oldLockTime = ( $oldLockTime / 60 ) + 1; # convert to minutes
	}

	if( $theAttachment ) {
	    ( $newLockUser, $newLockTime ) = &TWiki::Store::topicIsLockedBy( $newWeb, $newTopic );
	    if( $newLockUser ) {
	       $newLockUser = &TWiki::userToWikiName( $newLockUser );
	       use integer;
	       $newLockTime = ( $newLockTime / 60 ) + 1; # convert to minutes
	       my $editLock = $TWiki::editLockTime / 60;
	    }
	}
    }
    
    if( $oldLockUser || $newLockUser ) {
       my $tmpl = &TWiki::Store::readTemplate( "oopslockedrename" );
       my $editLock = $TWiki::editLockTime / 60;
       if( $oldLockUser ) {
           $tmpl =~ s/%OLD_LOCK%/Source topic $oldWeb.$oldTopic is locked by $oldLockUser, lock expires in $oldLockTime minutes.<BR>/go;
       } else {
           $tmpl =~ s/%OLD_LOCK%//go;
       }
       if( $newLockUser ) {
           $tmpl =~ s/%NEW_LOCK%/Destination topic $newWeb.$newTopic is locked by $newLockUser, lock expires in $newLockTime minutes.<BR>/go;
       } else {
           $tmpl =~ s/%NEW_LOCK%//go;
       }
       $tmpl =~ s/%NEW_WEB%/$newWeb/go;
       $tmpl =~ s/%NEW_TOPIC%/$newTopic/go;
       $tmpl =~ s/%ATTACHMENT%/$theAttachment/go;
       $tmpl = &TWiki::handleCommonTags( $tmpl, $oldTopic, $oldWeb );
       $tmpl = &TWiki::getRenderedVersion( $tmpl, $oldWeb );
       TWiki::writeHeader( $query );
       print $tmpl;
       return "";
    } else {
       &TWiki::Store::lockTopicNew( $oldWeb, $oldTopic );
       if( $theAttachment ) {
            &TWiki::Store::lockTopicNew( $newWeb, $newTopic );
       }
    }
    
    return "okay";
}


#============================
# Display screen so user can decide on new web and topic.
sub newTopicScreen
{
   my( $oldWeb, $oldTopic, $newWeb, $newTopic, $theAttachment, $confirm ) = @_;
   
   my $tmpl = "";
   
   if( ! $newTopic ) {
       $newTopic = $oldTopic;
   }
   
   TWiki::writeHeader( $query );
   if( $theAttachment ) {
     $tmpl = &TWiki::Store::readTemplate( "moveattachment" );
     $tmpl =~ s/%FILENAME%/$theAttachment/go;
   } elsif( $confirm ) {
     $tmpl = &TWiki::Store::readTemplate( "renameconfirm" );
   } else {
     $tmpl = &TWiki::Store::readTemplate( "rename" );
   }
   
   $tmpl = setVars( $tmpl, $newWeb, $newTopic );
   $tmpl = &TWiki::handleCommonTags( $tmpl, $oldTopic, $oldWeb );
   $tmpl = &TWiki::getRenderedVersion( $tmpl );
   print $tmpl;
 }
 
 #=========================
 sub setVars
 {
     my( $tmpl, $newWeb, $newTopic ) = @_;
     $tmpl =~ s/%NEW_WEB%/$newWeb/go;
     $tmpl =~ s/%NEW_TOPIC%/$newTopic/go;
     return $tmpl;
 }
 
 #=========================
 sub moreRefsToChange
 {
    my( $oldWeb, $oldTopic, $newWeb, $newTopic ) = @_;
    
    TWiki::writeHeader( $query );
    my $tmpl = &TWiki::Store::readTemplate( "renamerefs" );
    $tmpl = setVars( $tmpl, $newWeb, $newTopic );
    $tmpl = &TWiki::handleCommonTags( $tmpl, $oldTopic, $oldWeb );
    $tmpl = &TWiki::getRenderedVersion( $tmpl );
    print $tmpl;
 }

