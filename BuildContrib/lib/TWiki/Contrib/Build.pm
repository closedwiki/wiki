#
# Copyright (C) 2004 C-Dot Consultants - All rights reserved
#
# This program is free software; you can redistribute it and/or
# modify it under the terms of the GNU General Public License
# as published by the Free Software Foundation; either version 2
# of the License, or (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details, published at 
# http://www.gnu.org/copyleft/gpl.html
#
package TWiki::Contrib::Build;

use TWiki::Contrib::BuildContrib::BaseBuild;

=pod

---++ Package TWiki::Contrib::Build

This is a base class used for making build scripts for TWiki packages.
This class is derived from to create a build script for a specific module.

The resulting build script works a bit like make and a bit like Ant.
Build targets are Perl functions, which operate on various data defined
in control files to build the various targets. Perl is used rather than
make for portability reasons.

By default, the resulting build script will interpret the targets described
below, and the following options:
| -n | Do nothing |
| -v | Be verbose |

---+++ Targets

The following targets will always exist:
| build | *internal target* check that everything is perl |
| test | run unit tests |
| install | install on local installation defined by $TWIKI_HOME |
| uninstall | uninstall from local installation defined by $TWIKI_HOME |
| pod | build POD documentation of the package |
| release | build, pod and package a release zip |
| upload | build, pod, package and upload to twiki.org |
| manifest | print a guess at the MANIFEST to STDOUT |
Note: if you override any of these targets it is generally wise to call the SUPER version of the target!

---+++ Standard directory structure
The standard module directory structure mirrors the TWiki installation directory structure, so each file in the development directory structure is in the place it will be in in the actual installation. From the _root directory_, these are the key areas:
| lib/TWiki/Plugins/ | this is where your <code><i>name</i>.pm</code> file goes for plugins |
| lib/TWiki/Plugins/<i>name</i>/ | directory containing sub-modules used by your plugin, and your build.pl script. It is referred to below as the "module directory" |
Contribs are held in the =lib/TWiki/Contrib= directory instead of =lib/TWiki/Plugins= but otherwise work the same way.
| data/ | as you expect to see in the installation |
| pub/ | as you expect to see in the installation. You must list required directories, even if they are initially empty. |
| templates/ | as you expect to see in installation |
| templates/<skin name>/ | this is where templates for your skin go |
| contrib/ | this is where non-TWiki, non-web-accessible files associated with a Contrib or plugin go |

---+++ Environment Variables
   * =$TWIKI_LIBS= is a colon-separated path list that may be set to point at any required dependencies (usually CPAN dependencies). =$TWIKI_LIBS= is used to extend @INC for the duration of the build. If all dependencies are satisfied by directories on @INC it isn't required.
   * =$TWIKI_HOME= should point to the root of your test TWiki installation. =$TWIKI_HOME= is where the =install= target will run test installs. It is not needed if you never use the =install= target.

---+++ Manifest
The main driving file for the build is the MANIFEST file. This contains a list of all the files that are wanted in the zip file. It is located at build time by looking in the current directory, and if there is no file there looking up a level, and so on. It will normally be held in your _module directory_. The MANIFEST file consists of a list of file paths, each relative to the root of the installation. Wildcards may NOT be used. Each file has an optional octal permissions mask and a description; for example,
<verbatim>
data/TWiki/MyPlugin.txt 0664 Plugin description topic
</verbatim>
If no permissions are given, permissions are guessed from the permissions on the file in the source tree. These permissions are used by the installer script generated by the builder to set file permissions in the installation.

MANIFESTs can trigger the inclusion of other modules that have been wrapped using BuildContrib. For example,
<verbatim>
!include twikiplugins/WysiwygPlugin/lib/TWiki/Plugins/WysiwygPlugin
</verbatim>
This will use 'perl build.pl handsoff_install' to build and install the module in the release tree being built.

---+++ Dependencies
The DEPENDENCIES file specifies module dependencies, for example dependencies on other TWiki modules and on CPAN modules etc. It is found in the same way as the MANIFEST file. The DEPENDENCIES file contains a list of lines, each of which is a single dependency:
<verbatim>
name, version, type, description
</verbatim>
where
   * name is the name of the module,
   * version is the version constraint (e.g. ">1.5"),
   * type is its type (CPAN, perl, C etc) and
   * description is a short description of the module and where to get it.
Perl modules also referenced in the dependencies list in the stub topic should be listed using their perl package name (e.g. TWiki::Contrib::MyContrib) and use the type 'perl'.

A dependency may optionally be preceded by a condition that limits the cases where the dependency applies. The condition is specified using a line that contains <code>ONLYIF ( _condition_ )</code>, where _condition_ is a Perl conditional. This is most useful for enabling dependencies only for certain versions of TWiki. For example,
<verbatim>
TWiki::Rhinos,>=1.000,perl,Required. Download from TWiki:Plugins/RhinosContrib and install.
ONLYIF ($TWiki::Plugins::VERSION < 1.025)
TWiki::Plugins::CairoContrib, >=1.000, perl, Optional, only required if the plugin is to be run with versions of TWiki before Cairo. Available from the TWiki:Plugins/CairoContrib repository.
</verbatim>
Thus <nop>CairoContrib is only a dependency if the installation is being done on a TWiki version before Cairo. The ONLYIF only applies to the next dependency in the file.

The condition is included in the installer script generated by the builder.

---+++ Installer
The installer script generated by the builder when target =release= is used is based on a template installer. This template is populated with lists of files and dependencies to make the package-specific installer.

You can extend the installer by providing PREINSTALL, POSTINSTALL, PREUNINSTALL, and/or POSTUNINSTALL files. These optional files can contain Perl fragments that must execute at the given stage of the installation. The script fragments will be inserted into the generated installer script. Read contrib/TEMPLATE_installer.pl to see how they fit in. The POD comments in that module indicate the functions that are most likely to be useful to anyone writing a script extension. The files are found in the same was as the MANIFEST and DEPENDENCIES files.

---+++ Upload
The =upload= target will upload your package to TWiki.org. This is a two-stage process, because the forms on TWiki.org are edited there and are not part of the uploaded package. So the upload process first build a release package, then downloads the existing form from TWiki.org. Once the real topic is assembled it is uploaded again, and the new zip & tgz is attached.

---+++ Testing
The way tests are shipped in this version of BuildContrib has changed radically since TWiki 'Cairo' release. It is now possible to use a TWiki installation as a test fixture. =Test::Unit= tests are expected to be found in the =test/unit= directory in the root directory. Tests are run using the =tests/bin/TestRunner.pl= script (which is simply the =TestRunner.pl= script from the Test::Unit distribution, and will be present in any subversion checkout of TWiki). Tests will normally need a TWiki installation; normally you should use the TWiki pointed at by =$TWIKI_HOME= by including the following in the test module:
<verbatim>
BEGIN{
    push( @INC, "$ENV{TWIKI_HOME}/lib" );
}
</verbatim>

However this _only_ works with the 'Dakar' release (and later) of TWiki. Tests are bundled by the BuildContrib in a zip in the plugin directory, called tests.zip. This zip has to be unzipped from the root of the target installation before the tests can be run.

---+++ Token expansion
The build supports limited token expansion in =.txt= files. See the documentation on the =filter= method for more detail.

---+++ Methods

=cut

use strict;
use File::Copy;
use File::Spec;
use File::Find;
use Pod::Text;
use POSIX;
use CGI ( -any );
use diagnostics;
use Carp;
use vars qw( $VERSION $RELEASE $basedir $twiki_home $buildpldir $libpath );

# This should always be $Rev$ so that TWiki can determine the checked-in
# status of the plugin. It is used by the build automation tools, so
# you should leave it alone.
$VERSION = '$Rev$';

# This is a free-form string you can use to "name" your own plugin version.
# It is *not* used by the build automation tools, but is reported as part
# of the version number in PLUGINDESCRIPTIONS.
$RELEASE = 'Dakar';

my $NL = "\n";

$SIG{__DIE__} = sub { Carp::confess $_[0] };

sub _findRelativeTo {
    my( $startdir, $name ) = @_;

    my @path = split( /\/+/, $startdir );

    while (scalar(@path) > 0) {
        my $found = join( '/', @path).'/'.$name;
        return $found if -e $found;
        pop( @path );
    }
    return undef;
}

BEGIN {
    use File::Spec;
    $buildpldir = `dirname $0`; chop($buildpldir);
    $buildpldir = File::Spec->rel2abs($buildpldir);

    # Find the lib root
    $libpath = _findRelativeTo( $buildpldir, 'lib/TWiki' );
    die 'Could not find lib/TWiki' unless $libpath;
    $libpath =~ s#/[^/]*$##;

    $basedir = $libpath;
    $basedir =~ s#/[^/]*$##;

    $twiki_home = $ENV{'TWIKI_HOME'};
    if ($ENV{TWIKI_LIBS}) {
        my %known;
        map{ $known{$_} = 1 } split( /:/, @INC);
        foreach my $pc (reverse split(/:/,$ENV{TWIKI_LIBS})) {
            unless( $known{$pc} ) {
                unshift(@INC, $pc);
            }
        }
    }
    unless(grep(/$basedir\/lib/,@INC)) {
        unshift(@INC, $basedir.'/lib');
    }
}

=pod

---++++ new($project)
| $project | Name of plugin, addon, contrib or skin |
| $rootModule | Optional, if defined gives the name of the root .pm module that carries the VERSION and dependencies. Defaults to $project |
Construct a new build object. Define the basic directory
paths to places in the build/release. Read the manifest topic
and build file and dependency lists. Parse command line to get
target and options.

=cut
sub new {
    my ( $class, $project, $rootModule ) = @_;
    my $this = bless({}, $class);

    $this->{project} = $project;
    $this->{target} = 'test';

    my $n = 0;
    my $done = 0;
    while ($n <= $#ARGV) {
        if ($ARGV[$n] =~ /^-/o) {
            $this->{$ARGV[$n]} = 1;
        } else {
            $this->{target} = $ARGV[$n];
        }
        $n++;
    }
    if ($this->{-v}) {
        print 'Building in ',$buildpldir,$NL;
        print 'Root module is  ',$rootModule,$NL if $rootModule;
        print 'Basedir is ',$basedir,$NL;
        print 'Component dir is ',$libpath,$NL;
        print 'Using path '.join(':',@INC).$NL;
    }

    $this->{basedir} = $basedir;

    # The following paths are all relative to the root of the twiki
    # installation

    #SMELL: Hardcoded project classification
    # where the sub-modules live
    $this->{libdir} = $libpath;
    if( $this->{project} =~ /Plugin$/ ) {
        $this->{libdir} .= '/TWiki/Plugins';
    } elsif( $this->{project} =~ /Contrib$/ ) {
        $this->{libdir} .= '/TWiki/Contrib';
    }

    # the .pm module
    if ($rootModule) {
        $this->{pm} = $this->{libdir}.'/'.$rootModule.'.pm';
    } else {
        $this->{pm} = $this->{libdir}.'/'.$project.'.pm';
    }

    my $stubpath = $this->{pm};
    $stubpath =~ s/.*[\\\/](TWiki[\\\/].*)\.pm/$1/;
    $stubpath =~ s/[\\\/]/::/g;
    $this->{STUB} = $stubpath;

    # where data files live
    $this->{data_twiki} = 'data/TWiki';

    # the root of the name of data files
    $this->{data_twiki_module} = $this->{data_twiki}.'/'.$this->{project};

    ##############################################################
    # Read the manifest

    my $manifest = _findRelativeTo( $buildpldir, 'MANIFEST' );
    ($this->{files},$this->{other_modules})=readManifest($this->{basedir},'',$manifest,sub{exit(1)});

    # Add the install script to the manifest, unless it is already there
    unless( grep(/^$this->{project}_installer.pl$/,
                 map {$_->{name}} @{$this->{files}})) {
        push(@{$this->{files}},
             { name => $this->{project}.'_installer.pl',
               description => 'Install script',
               permissions => 0440 });
        print STDERR 'Auto-adding install script to manifest',$NL;
    }

    # Generate a TWiki table representing the manifest contents
    # and a hash table representing the files
    my $mantable = '';
    my $hashtable = '';
    foreach my $file (@{$this->{files}}) {
        $mantable .= "\t| ==" . $file->{name} . '== | ' .
          $file->{description} . ' |'.$NL;
        $hashtable .= "'$file->{name}'=>$file->{permissions},";
    }
    $this->{MANIFEST} = $mantable;
    $this->{FILES} = $hashtable;

    ##############################################################
    # Work out the dependencies

    my $deps = _findRelativeTo($buildpldir, 'DEPENDENCIES');
    die 'Failed to find DEPENDENCIES' unless $deps && -f $deps;
    my $condition = '';
    if (-f $deps) {
        open(PF, '<'.$deps) ||
          die 'Failed to open '.$deps;
        while (my $line = <PF>) {
            if ($line =~ /^ONLYIF\s*(\(.*\))\s*$/) {
                $condition = $1;
            } elsif ($line =~ m/^(\w+)\s+(\w*)\s*(.*)$/o) {
                push(@{$this->{dependencies}},
                     { name=>$1, type=>$2, version=>'',
                       description=>$3, trigger=>$condition});
                $condition='';
            } elsif ($line =~ m/^([^,]+),([^,]*),\s*(\w*)\s*,\s*(.+)$/o) {
                push(@{$this->{dependencies}},
                     { name=>$1, version=>$2, type=>$3, description=>$4,
                       trigger=>$condition });
                $condition='';
            } elsif ($line !~ /^\s*$/ && $line !~ /^\s*#/) {
                warn 'WARNING: LINE '.$line.' IN '.$deps.' IGNORED';
            }
        }
    } else {
        warn 'WARNING: no '.$deps.'; dependencies will only be extracted from code';
    }
    close(PF);

    my $version = 'Unknown';
    if (open(PF,'<'.$this->{pm})) {
        my $text = '';
        while (my $line = <PF>) {
            $line =~ s/\s+//g;
            $text .= $line.$NL;
        }
        if ($text =~ /^\$VERSION\s*=(.*?);$/m) {
            $version = $1;
            if ($version =~ /\$Rev/ ) {
                $version = _get_svn_version($this->{pm});
            }
        }
    } else {
        warn 'WARNING: '.$this->{pm}.
          ' not found; cannot extract VERSION';
    }
    close(PF);

    my $deptable = '';
    my $a = ' align="left"';
    foreach my $dep (@{$this->{dependencies}}) {
        my $v = $dep->{version};
        $v =~ s/&/&amp;/go;
        $v =~ s/>/&gt;/go;
        $v =~ s/</&lt;/go;
        my $cells = CGI::td({align=>'left'}, $dep->{name} ).
          CGI::td({align=>'left'}, $dep->{description});
        $deptable .= CGI::Tr( $cells );
    }
    $this->{DEPENDENCIES} = 'None';
    if ($deptable) {
        my $cells = CGI::th('Name').CGI::th('Version').CGI::th('Description');
        $this->{DEPENDENCIES} =
          CGI::table({ border=>1}, CGI::Tr($cells).$deptable );
    }
    $this->{VERSION} = $version;
    $this->{DATE} = POSIX::strftime('%T %d %B %Y', localtime);

    local $/ = undef;
    foreach my $stage ( 'PREINSTALL', 'POSTINSTALL', 'PREUNINSTALL', 'POSTUNINSTALL' ) {
        $this->{$stage} = '# No '.$stage.' script';
        my $file = _findRelativeTo($buildpldir, $stage);
        if ($file && open(PF, '<'.$file)) {
            $this->{$stage} = <PF>;
        }
    }

    $this->{MODULE} = $this->{project};

    return $this;
}

sub _get_svn_version {
    my $from = shift;
    my $ver = `svn info $from`;
    unless (!$ver ) {
        if( $ver =~ /Revision: (\d+)/s ) {
            return $1;
        }
    }
    return undef;
}

=pod

---++++ cd($dir)
  Change to the given directory

=cut

sub cd {
    my ($this, $file) = @_;

    if ($this->{-v} || $this->{-n}) {
        print 'cd '.$file.$NL;
    }
    if (!$this->{-n}) {
        chdir($file) || die 'Failed to cd to '.$file;
    }
}

=pod

---++++ rm($file)
Remove the given file

=cut

sub rm {
    my ($this, $file) = @_;

    if ($this->{-v} || $this->{-n}) {
        print 'rm '.$file.$NL;
    }
    if (-e $file && !$this->{-n}) {
        unlink($file) || warn 'WARNING: Failed to delete '.$file;
    }
}

=pod

---++++ makepath($to)
Make a directory and all directories leading to it.

=cut

sub makepath {
    my ($this, $to) = @_;

    chop($to) if ($to =~ /\n$/o);

    return if (-d $to || $this->{made_dir}->{$to});
    $this->{made_dir}->{$to} = 1;

    if (! -e $to) {
        $this->makepath(`dirname $to`);
        if ($this->{-v} || $this->{-n}) {
            print 'mkdir '.$to.$NL;
        }
        unless ($this->{-n}) {
            mkdir($to) || warn 'Warning: Failed to make '.$to.': '.$!;
        }
    } else {
        warn 'Warning: '.$to.' exists and is not a directory; cannot create a dir over it';
    }
}

=pod

---++++ cp($from, $to)
Copy a single file from - to. Will automatically make intervening
directories in the target. Also works for target directories.

=cut

sub cp {
    my ($this, $from, $to) = @_;

    die 'Source file '.$from.' does not exist ' unless ( $this->{-n} || -e $from);

    $this->makepath(`dirname $to`);

    if ($this->{-v} || $this->{-n}) {
        print 'cp '.$from.' '.$to.$NL;
    }
    unless ($this->{-n}) {
        if ( -d $from ) {
            mkdir($to) || warn 'Warning: Failed to make '.$to.': '.$!;
        } else {
            File::Copy::copy($from, $to) ||
                warn 'Warning: Failed to copy '.$from.' to '.$to.': '.$!;
        }
    }
}

=pod

---++++ prot($perms, $file)
Set permissions on a file. Permissions should be expressed using POSIX
chmod notation.

=cut

sub prot {
    my ($this, $perms, $file) = @_;
    $this->perl_action("chmod($perms,'$file')");
}

=pod

---++++ sys_action($cmd)
Perform a "system" command.

=cut

sub sys_action {
    my ($this, $cmd) = @_;

    if ($this->{-v} || $this->{-n}) {
        print $cmd.$NL;
    }
    unless ($this->{-n}) {
        system($cmd);
        die 'Failed to '.$cmd.': '.$? if ($?);
    }
}

=pod

---++++ perl_action($cmd)
Perform a "perl" command.

=cut

sub perl_action {
    my ($this, $cmd) = @_;

    if ($this->{-v} || $this->{-n}) {
        print $cmd.$NL;
    }
    unless ($this->{-n}) {
        eval $cmd;
        die 'Failed to '.$cmd.': '.$@ if ($@);
    }
}

=pod

---++++ target_build
Basic build target. By default does nothing, but subclasses may want to
extend on that.

=cut

sub target_build {
    my $this = shift;
    # does nothing
}

=pod

---++++ target_test
Basic Test::Unit test target, runs <project>Suite.

=cut

sub target_test {
    my $this = shift;
    $this->build('build');

    # find testrunner
    my $testrunner = _findRelativeTo($this->{basedir},
                                     'test/bin/TestRunner.pl');

    my $tests = _findRelativeTo($this->{basedir}, 'test/unit/'.
                                  $this->{project}.'/'.
                                    $this->{project}.'Suite.pm');
    unless( $tests ) {
        $tests = _findRelativeTo($this->{basedir}, '/test/unit/'.
                                   $this->{project}.'Suite.pm');
        unless( $tests ) {
            warn 'WARNING: COULD NOT FIND ANY UNIT TESTS FOR '.
              $this->{project};
            return;
        }
    }
    unless($testrunner) {
        warn 'WARNING: CANNOT RUN TESTS; TestRunner.pl not found';
        return;
    }
    my $inc = join(' -I', @INC);
    my $testdir = $tests;
    $testdir =~ s/\/[^\/]*$//;
    print "Running tests in $tests\n";
    $this->cd($testdir);
    $this->sys_action('perl -w -I'.$inc.' '.$testrunner.' '.$tests);
    shift( @INC );
}

=pod

---++++ filter_txt
Expands tokens. The following tokens are supported:
   * %$MANIFEST% - TWiki table of files in MANIFEST
   * %$FILES% - hash keyed on file name mapping to permissions i.e. 'data/TWiki/ThsiTopic.txt' => 0664, 'lib/TWiki/Plugins/BlahPlugin.pm' => 0775
   * %$DEPENDENCIES% - list of dependencies from DEPENDENCIES
   * %$VERSION% version from $VERSION in main .pm
   * %$DATE% - local date
   * %$POD% - expands to the POD documentation for the package, excluding test modules.
   * %$PREINSTALL% - inserts script from PREINSTALL
   * %$POSTINSTALL% - inserts script from POSTINSTALL
   * %$PREUNINSTALL% - inserts script from PREUNINSTALL
   * %$POSTUNINSTALL% - inserts script from POSTINSTALL
Three spaces is automatically translated to tab.

The filter is used in the generation of documentation topics and the installer

=cut

sub filter_txt {
    my ($this, $from, $to) = @_;

    return unless (-f $from);

    open(IF, '<'.$from) || die 'No source topic '.$from.' for filter';
    local $/ = undef;
    my $text = <IF>;
    close(IF);
    $text =~ s/%\$(\w+)%/&_expand($this,$1)/geo;
    $text =~ s/ {3}/\t/g;

    unless ($this->{-n}) {
        open(OF, '>'.$to) || die 'No dest topic '.$to.' for filter';
    }
    print OF $text unless ($this->{-n});
    close(OF) unless ($this->{-n});
}

sub _expand {
    my ($this, $tok) = @_;
    if (!$this->{$tok} && $tok eq 'POD') {
        $this->build('pod');
    }
    if (defined($this->{$tok})) {
        if ($this->{-v} || $this->{-n}) {
            print 'expand %$'.$tok.'% to '.$this->{$tok}.$NL;
        }
        return $this->{$tok};
    } else {
        return '%$'.$tok.'%';
    }
}

=pod

---++++ filter_pm($from, $to)
Filters expanding SVN rev number with correct version from repository

=cut

sub filter_pm {
    my ($this, $from, $to) = @_;

    open(IF, '<'.$from) || die 'No source topic '.$from.' for filter';
    local $/ = undef;
    my $text = <IF>;
    close(IF);

    my $ver = _get_svn_version($from);
    if( $ver ) {
        if( $text =~ s/\$Rev(: \d+)?\$/\$Rev: $ver\$/gso ) {
        }
    }

    unless ($this->{-n}) {
        open(OF, '>'.$to) || die 'Bad dest topic '.$to.' for filter:'.$!;
        print OF $text;
        close(OF);
    }
}

=pod

---++++ target_release
Release target, builds release zip by creating a full release directory
structure in /tmp and then zipping it in one go. Only files explicitly listed
in the MANIFEST are released. Automatically runs =filter= on all =.txt= files
in the MANIFEST.

=cut

sub target_release {
    my $this = shift;

    $this->build('build');
    $this->build('installer');
    $this->build('stage');
    $this->build('archive');
}

=pod

---++++ target_stage
stages all the files to be in the release in a tmpDir, ready for target_archive

=cut

sub target_stage {
    my $this = shift;
    my $project = $this->{project};

    $this->{tmpDir} = '/tmp/'.$$;
    $this->makepath($this->{tmpDir});

    $this->copy_fileset($this->{files}, $this->{basedir}, $this->{tmpDir});
    foreach my $file (@{$this->{files}}) {
        if ($file->{name} =~ /\.txt$/) {
            my $txt = $file->{name};
            $this->filter_txt($this->{basedir}.'/'.$txt, $this->{tmpDir}.'/'.$txt);
        } elsif (
            $file->{name} =~ /\.pm$/) {
            my $txt = $file->{name};
            $this->filter_pm($this->{basedir}.'/'.$txt, $this->{tmpDir}.'/'.$txt);
        }
    }
    if( -e $this->{tmpDir}.'/'.$this->{data_twiki_module}.'.txt' ) {
        $this->cp($this->{tmpDir}.'/'.$this->{data_twiki_module}.'.txt',
                  $this->{basedir}.'/'.$project.'.txt');
    }
    $this->apply_perms($this->{files}, $this->{tmpDir} );

    if( $this->{other_modules} ) {
        my $libs = join(':',@INC);
        foreach my $module (@{$this->{other_modules}}) {
            print STDERR "Installing $module in $this->{tmpDir}\n";
            print `export TWIKI_HOME=$this->{tmpDir}; export TWIKI_LIBS=$libs; cd $basedir/$module; perl build.pl handsoff_install`;
        }
    }
}

=pod

---++++ target_archive
makes zip and tgz archives of the files in tmpDir

=cut

sub target_archive {
    my $this = shift;
    my $project = $this->{project};

    die 'no tmpDir set' unless defined ($this->{tmpDir});
    die 'no project set' unless defined ($project);
    die 'tmpDir ('.$this->{tmpDir}.') not found' unless ( -e $this->{tmpDir} );

    $this->cd($this->{tmpDir});
    $this->sys_action('zip -r -q '.$project.'.zip *');
    $this->perl_action('File::Copy::move("'.$this->{tmpDir}.'/'.$project.'.zip", "'.
                         $this->{basedir}.'/'.$project.'.zip");');
    $this->sys_action('tar czpf '.$project.'.tgz *');
    $this->perl_action('File::Copy::move("'.$this->{tmpDir}.'/'.$project.'.tgz", "'.
                         $this->{basedir}.'/'.$project.'.tgz")');

    $this->sys_action('md5sum '.$this->{basedir}.'/'.$project.'.tgz '.
                        $this->{basedir}.'/'.$project.'.zip > '.
                        $this->{basedir}.'/'.$project.'.md5');


    print 'Release ZIP is '.$this->{basedir}.'/'.$project.'.zip',$NL;
    print 'Release TGZ is '.$this->{basedir}.'/'.$project.'.tgz',$NL;
    print 'Release TOPIC is '.$this->{basedir}.'/'.$project.'.txt',$NL;
    print 'MD5 checksums are in '.$this->{basedir}.'/'.$project.'.md5',$NL;
}

=pod

---++++ copy_fileset
Copy all files in a file set from on directory root to another.

=cut

sub copy_fileset {
    my ($this, $set, $from, $to) = @_;

    my $uncopied = scalar(@$set);
    if ($this->{-v} || $this->{-n}) {
        print 'Copying '.$uncopied.' files to '.$to.$NL;
    }
    foreach my $file (@$set) {
        my $name = $file->{name};
        if (! -e $from.'/'.$name) {
            die $from.'/'.$name.' does not exist - cannot copy';
        }
        $this->cp($from.'/'.$name, $to.'/'.$name);
        $uncopied--;
    }
    die 'Files left uncopied' if ($uncopied);
}

=pod

---++++ apply_perms
Apply perms to a fileset

=cut

sub apply_perms {
    my ($this, $set, $to) = @_;

    foreach my $file (@$set) {
        my $name = $file->{name};
        if( defined $file->{permissions} ) {
            $this->prot($file->{permissions}, $to.'/'.$name);
        }
    }
}

=pod

---++++ target_handsoff_install
Install target, installs to local twiki pointed at by TWIKI_HOME.

Does not run the installer script.

=cut

sub target_handsoff_install {
    my $this = shift;
    $this->build('release');

    my $twiki = $ENV{TWIKI_HOME};
    die 'TWIKI_HOME not set' unless $twiki;
    $this->cd($twiki);
    $this->sys_action('unzip -u -o '.$this->{basedir}.'/'.
                        $this->{project}.'.zip');
    # kill off the module installer
    $this->rm($twiki.'/'.$this->{project}.'_installer.pl');
}

=pod

---++++ target_install
Install target, installs to local twiki pointed at by TWIKI_HOME.

Uses the installer script written by target_installer

=cut

sub target_install {
    my $this = shift;
    $this->build('handsoff_install');
    $this->sys_action('perl '.$this->{project}.'_installer.pl install');
}

=pod

---++++ target_uninstall
Uninstall target, uninstall from local twiki pointed at by TWIKI_HOME.

Uses the installer script written by target_installer

=cut

sub target_uninstall {
    my $this = shift;
    my $twiki = $ENV{TWIKI_HOME};
    die 'TWIKI_HOME not set' unless $twiki;
    $this->cd($twiki);
    $this->sys_action('perl '.$this->{project}.'_installer.pl uninstall');
}

{   package TWiki::Contrib::Build::UserAgent;
    @TWiki::Contrib::Build::UserAgent::ISA = qw(LWP::UserAgent);

    my ( $knownUser, $knownPass );

    sub get_basic_credentials {
        my($self, $realm, $uri) = @_;
        unless ( $knownUser ) {
            print 'Logon to ',$uri->host_port,$NL;
            print 'Enter ',$realm,': ';
            $knownUser = <STDIN>;
            chomp($knownUser);
            return (undef, undef) unless length $knownUser;
            print 'Password on ',$uri->host_port,': ';
            system('stty -echo');
            $knownPass = <STDIN>;
            system('stty echo');
            print $NL;  # because we disabled echo
            chomp($knownPass);
        }
        return ($knownUser, $knownPass);
    }
}

=pod

---++++ target_upload
Upload to twiki.org. Prompts for username and password. Uploads the zip and
the text topic to the appropriate places. Creates the topic on twiki.org if
necessary.

=cut

sub target_upload {
    my $this = shift;
    $this->build('release');

    require LWP;
    if ( $@ ) {
        print STDERR 'LWP is not installed; cannot upload',$NL;
        return 0;
    }

    my $userAgent = TWiki::Contrib::Build::UserAgent->new();
    $userAgent->agent( 'TWikiContribBuild/'.$VERSION.' ' );

    my $to = $this->{project};

    # Get the old form data and attach it to the update
    print 'Downloading http://twiki.org/cgi-bin/view/Plugins/'.$to.' to recover form',$NL;
    my $response = $userAgent->get( 'http://twiki.org/cgi-bin/view/Plugins/'.$to.'?raw=debug' );

    die 'Failed to GET old plugins topic ', $response->request->uri,
      ' -- ', $response->status_line, $NL.'Aborting'
        unless $response->is_success;
    my %newform;
    foreach my $line ( split(/\n/, $response->content() )) {
        if ( $line =~ m/META:FIELD{name="(.*?)".*?value="(.*?)"}/ ) {
            my $val = $2;
            if ($val && $val ne '') {
                $newform{$1} = $val;
            }
        }
    }
    local $/ = undef; # set to read to EOF
    open( IN_FILE, '<'.$this->{basedir}.'/'.$to.'.txt' ) or
      die 'Failed to reopen topic: '.$@;
    $newform{'text'} = <IN_FILE>;
    close( IN_FILE );

    print 'Uploading new topic',$NL;
    $response = $userAgent->post( 'http://twiki.org/cgi-bin/save/Plugins/'.$to,
                                  \%newform );

    die 'Update of topic failed ', $response->request->uri,
      ' -- ', $response->status_line, 'Aborting'
        unless $response->is_redirect &&
          $response->headers->header('Location') =~ /view([\.\w]*)\/Plugins\/$to/;

    print 'Uploading zip',$NL;
    $response =
      $userAgent->post( 'http://twiki.org/cgi-bin/upload/Plugins/'.$to,
                        [
                         'filename' => $to.'.zip',
                         'filepath' => [ $this->{basedir}.'/'.$to.'.zip' ],
                         'filecomment' => 'unzip in the root directory of your TWiki installation and run the installer script'
                        ],
                        'Content_Type' => 'form-data' );

    die 'Update of zip failed ', $response->request->uri,
      ' -- ', $response->status_line, $NL, 'Aborting',$NL, $response->as_string
        unless $response->is_redirect &&
          $response->headers->header('Location') =~ /view([\.\w]*)\/Plugins\/$to/;

    print 'Uploading tgz',$NL;
    $response =
      $userAgent->post( 'http://twiki.org/cgi-bin/upload/Plugins/'.$to,
                        [
                         'filename' => $to.'.tgz',
                         'filepath' => [ $this->{basedir}.'/'.$to.'.tgz' ],
                         'filecomment' => 'Untar in the root directory of your TWiki installation and run the installer script'
                        ],
                        'Content_Type' => 'form-data' );

    die 'Update of tgz failed ', $response->request->uri,
      ' -- ', $response->status_line, $NL, 'Aborting',$NL, $response->as_string
        unless $response->is_redirect &&
          $response->headers->header('Location') =~ /view([\.\w]*)\/Plugins\/$to/;

    print 'Uploading md5 checksums',$NL;
    $response =
      $userAgent->post( 'http://twiki.org/cgi-bin/upload/Plugins/'.$to,
                        [
                         'filename' => $to.'.md5',
                         'filepath' => [ $this->{basedir}.'/'.$to.'.md5' ],
                         'filecomment' => 'md5 checksums'
                        ],
                        'Content_Type' => 'form-data' );

    die 'Update of zip failed ', $response->request->uri,
      ' -- ', $response->status_line, $NL, 'Aborting',$NL, $response->as_string
        unless $response->is_redirect &&
          $response->headers->header('Location') =~ /view([\.\w]*)\/Plugins\/$to/;

}

sub _unhtml {
    my $html = shift;
    
    $html =~ s/<[^<>]*>//og;
    $html =~ s/&\w+;//go;
    $html =~ s/\s//go;
    
    return $html;
}

=pod

---++++ target_pod

Build POD documentation. This target defines =%$POD%= - it
does not generate any output files. The target will be invoked
automatically if =%$POD%= is used in a .txt file. POD documentation
is intended for use by developers only.

POD test in =.pm= files should use TWiki syntax or HTML. Packages should be
introduced with a level 0 header, and each method in the package by
a second level header. Make sure you document any global variables used
by the module.

=cut

sub target_pod {
    my $this = shift;
    my $tmpfile = '/tmp/buildpod';
    $this->{POD} = '';

    foreach my $file (@{$this->{files}}) {
        my $pmfile = $file->{name};
        if ($pmfile =~ /\.pm$/o) {
            $pmfile = $this->{basedir}.'/'.$pmfile;
            my $parser = new Pod::Text(indent => 0);
            $parser->parse_from_file($pmfile, $tmpfile);
            open(TMP, $tmpfile);
            while (<TMP>) {
                $this->{POD} .= $_;
            }
            close(TMP);
        }
    }
    unlink($tmpfile);
}

=pod

---++++ target_installer

Write an install/uninstall script that checks dependencies, and optionally
downloads and installs required zips from twiki.org.

The install script is templated from =contrib/TEMPLATE_installer= and
is always named =module_installer.pl= (where module is your module). It is
added to the release zip and is always shipped in the root directory.
It will automatically be added to the manifest if it doesn't appear in
MANIFEST.

The install script works using the dependency type and version fields.
It will try to download from twiki.org to satisfy any missing dependencies.
Downloaded modules are automatically installed.

Note that the dependencies will only work if the module depended on follows
the naming standards for zips i.e. it must be attached to the topic in
twiki.org and have the same name as the topic, and must be a zip file.

Dependencies on CPAN modules are also checked (type perl) but no attempt
is made to install them.

The install script also acts as an uninstaller and upgrade script.

__Note__ that =target_install= builds and invokes this install script.

At present there is no support for a caller-provided post-install script, but
this would be straightforward to do if it were required.

=cut

sub target_installer {
    my $this = shift;

    # Find the template on @INC
    my $template;
    foreach my $d ( @INC ) {
        my $dir = `dirname $d`;
        chop($dir);
        my $file = $dir.'/lib/TWiki/Contrib/BuildContrib/TEMPLATE_installer.pl';
        if ( -f $file ) {
            $template = $file;
            last;
        }
        $dir .= '/contrib';
        if ( -f $dir.'/TEMPLATE_installer.pl' ) {
            $template = $dir.'/TEMPLATE_installer.pl';
            last;
        }
    }
    unless($template) {
        die 'COULD NOT LOCATE TEMPLATE_installer.pl - required for install script generation';
    }

    my @sats;
    foreach my $dep (@{$this->{dependencies}}) {
        my $descr = $dep->{description};
        $descr =~ s/"/\\\"/g;
        $descr =~ s/\$/\\\$/g;
        $descr =~ s/\@/\\\@/g;
        $descr =~ s/\%/\\\%/g;
        my $trig = $dep->{trigger};
        $trig = 1 unless ( $trig );
        push(@sats, "{ name=>'$dep->{name}', type=>'$dep->{type}',version=>'$dep->{version}',description=>'$descr', trigger=>$trig }");
    }
    my $satisfies = join("\n,", @sats);
    $this->{SATISFIES} = $satisfies;

    my $installScript = $this->{basedir}.'/'.$this->{project}.'_installer.pl';
    if ($this->{-v} || $this->{-n}) {
        print 'Generating installer in ',$installScript,$NL;
    }

    $this->filter_txt( $template, $installScript );

    $this->prot(0755, $installScript);
}

=pod

---++++ build($target)
Build the given target

=cut

sub build {
    my $this = shift;
    my $target = shift;
    if ($this->{-v}) {
        print 'Building ',$target,$NL;
    }
    my $fn = 'target_'.$target;
    no strict "refs";
    $this->$fn();
    use strict "refs";
    if ($@) {
        die 'Failed to build ',$target,': ',$@;
    }
    if ($this->{-v}) {
        print 'Built ',$target,$NL;
    }
}

=pod

---++++ target_manifest
Generate and print to STDOUT a rough guess at the MANIFEST listing

=cut

my %manilist;
my $rootdir;
sub target_manifest {
    my $this = shift;

    my $manifest = _findRelativeTo($buildpldir,'MANIFEST');
    if( $manifest && -e $manifest ) {
        open(F, '<'.$manifest) || die 'Could not open existing '.$manifest;
        local $/ = undef;
        %manilist = map{ /^(.*?)(\s+.*)?$/; $1 => ($2||'') } split(/\r?\n/, <F> );
        close(F);
    } else {
        $manifest = $buildpldir.'/MANIFEST';
    }
    require File::Find;

    $rootdir = $this->{basedir};
    print STDERR "Gathering from $rootdir\n";

    File::Find::find(\&_manicollect, $rootdir);
    print '# DRAFT ',$manifest,' follows:',$NL;
    print '################################################',$NL;
    for (sort keys %manilist) {
        print $_.' '.$manilist{$_}.$NL;
    }
    print '################################################',$NL;
    print '# Copy and paste the text between the ###### lines into the file',$NL;
    print '# '.$manifest,$NL;
    print '# to create an initial manifest. Remove any files',$NL;
    print '# that should _not_ be released (such as build.pl!), and add a',$NL;
    print '# description of each file in place of NEW.',$NL;
}

sub _manicollect {
    if( /^(CVS|\.svn|twikiplugins)$/ ) {
        $File::Find::prune = 1;
    } elsif ( !-d && /^\w.*\w$/ &&
                !/^DEPENDENCIES$/ &&
                  !/^MANIFEST$/ &&
                    !/^[A-Z]+INSTALL$/ &&
                      !/^build.pl$/ ) {
        my $n = $File::Find::name;
        $n =~ s/$rootdir\/?//;
        $manilist{$n} = 'NEW' unless exists $manilist{$n};
    }
}

1;
